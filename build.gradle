plugins {
    id "application"
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.th2.component)
}

group = 'com.exactpro.th2'
version = release_version

repositories {
    mavenCentral()
    mavenLocal()
    maven { // FIXME: remove after test
        name 'Sonatype_snapshot '
        url 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

dependencies {
    implementation(libs.th2.common)
    implementation(libs.th2.common.utils)
    implementation(libs.th2.grpc.act)
    implementation(libs.th2.grpc.check1)

    implementation(libs.kotlin.logging)

    implementation("com.fasterxml.jackson.core:jackson-core")
    implementation("com.fasterxml.jackson.core:jackson-databind")

    implementation("org.slf4j:slf4j-api")

    implementation("org.apache.commons:commons-lang3")

    testImplementation(platform(libs.junit.bom))
    testImplementation(libs.strikt.core)
    testImplementation(libs.mockito.kotlin)
    testImplementation(libs.th2.junit.jupiter.integration)
    testImplementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml")
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.junit.platform:junit-platform-launcher") {
        because 'This artifact will not be added automatically since gradle 9.0. You can read more information here:' +
                'https://docs.gradle.org/8.3/userguide/upgrading_version_8.html#test_framework_implementation_dependencies'
    }
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit5")
    testImplementation testFixtures(libs.th2.common)
}

application.mainClass = "com.exactpro.th2.act.ActMain"
dependencyCheck.suppressionFile = "suppressions.xml"

tasks.test {
    useJUnitPlatform {
        excludeTags("integration-test")
    }
}

tasks.register("integrationTest", Test) {
    group = "verification"
    useJUnitPlatform {
        includeTags("integration-test")
    }
    testLogging {
        showStandardStreams = true
    }

    if (isPodmanInstalled()) {
        environment("DOCKER_HOST", "unix:///run/user/${getUid()}/podman/podman.sock")
        environment("TESTCONTAINERS_RYUK_DISABLED", "true")
    }
}

static def isPodmanInstalled() {
    try {
        def process = new ProcessBuilder("podman", "--version").start()
        process.waitFor()
        return process.exitValue() == 0
    } catch (IOException ignored) {
        println("podman isn't installed")
        return false
    }
}

static def getUid() {
    new ProcessBuilder("id", "-u").start().inputStream.text.trim()
}